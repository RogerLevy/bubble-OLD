: extents  0 0 4096 4096 ;

\ engine base
include engine\saturn\gameorama

\ graphics services
include engine\modules\swes\sprites
\ include engine\modules\swes\tilesets
\ include engine\modules\swes\tilemap
\ include engine\modules\swes\tilemap-collision
\ include engine\modules\swes\layers

\ load other modules
include engine\modules\stride2d
include engine\modules\collision-grid
include engine\modules\gameutils
include engine\modules\wallpaper
include engine\modules\tiled-level

\ load engine specific stuff
include engine\saturn\scripting.f
include engine\saturn\objects.f
include engine\saturn\physics.f
include engine\saturn\box.f



:noname [ is oneInit ]
  at@  startx 2v!
  1 1 1 1 !color
  csolid# cmask !
  32768 zdepth !
;


\ constants
actor single cam
actor single player

\ variables
variable 'dialog  \ for now this is just a flag.

\ automatically load images and sounds.
include engine\saturn\autodata

\ game-specific data
include data

\ more engine specific stuff
include engine\saturn\load.f

fixed


player as  " traveler" script become
  100 100 player put


\ -----------------------------------------------------------------------------

\ bubble stuff

include obj\bubble

\ :proc bubbly  player 's x 2v@ at  me  bubble one  -1 vy !  as ;
:task bubbly
  begin  player 's x 2v@ at  me  bubble one   1 2 rnd + expire
    -1 vy !  as  3 frames again ;


variable bgbubbles  bgbubbles on

: *bgbubble
  cam 's x 2v@  0 gfxh 2+  -70 10 2+  gfxw 140 + 0  somewhere at
  me  bubble one  -1 vy !  1 0 0 1 !color  -100 zdepth +!    0 cflags !  0 cmask !
  as ;

:task bubblefx
  begin  ( 20 50 rnd + ) 1 frames  bgbubbles @ if  *bgbubble  then  again ;

: ?pointcull
  x 2v@ 2dup  cam 's x 2v@ 80 80 2-  gfxw gfxh 160 160 2+  2over 2+
  overlap? ?exit  me unload ;

' ?pointcull bubble 'cull !

: cull  0 stage all>  me class @ 'cull @ execute ;


\ -----------------------------------------------------------------------------

include engine\saturn\zones.f

\ --------------------------- camera/rendering --------------------------------

transform baseline

: /baseline  ( -- )
  baseline  al_identity_transform
  baseline  factor @ dup 2af  al_scale_transform
  baseline  al_use_transform  ;


\ camera stuff

create m  16 cells /allot

: camTransform  ( -- matrix )
  m al_identity_transform
  m  cam 's x 2v@ 2pfloor 2negate 2af  al_translate_transform
  m ;

: track ( -- )
  player 's x 2v@  player 's w 2v@ 2halve  2+
  gfxw gfxh 2halve  2-  extents 2clamp  cam 's x 2v! ;

: camview
  camTransform  dup  factor @ dup 2af  al_scale_transform
  al_use_transform ;


\ depth sorting

: enqueue  me , ;
: showem  ( addr -- addr )  here over ?do  i @ as  show  cell +loop ;
: @zdepth  [ zdepth me - ]# + @ ;
: sort  dup here over - cell/ s>p  ['] @zdepth irsort ;
: vfilter  0 stage all>  vis @ -exit  enqueue ;
: sorted  here  vfilter  sort  showem  reclaim ;


\ rendering

: para  ;
: batch  al_hold_bitmap_drawing ;
: cls  0 0 0 1 clear-to-color ;
: overlays  ;
: all  0 stage all>  show ;
: boxes  info @ -exit  0 stage all>  showCbox ;
: camRender
  cls  /baseline
  para  track  camview
  1 batch  sorted  overlays  0 batch
  boxes ;


\ bring the logic together

: logic  0 stage all> act ;
: saturnSim  ( physics ) zones  logic  multi  cull  sweep  1 +to #frames ;


\ piston config

' camRender is render
' saturnSim is sim


